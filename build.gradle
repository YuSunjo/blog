buildscript {
    ext {
        kotlinVersion = '1.5.21'
        springBootVersion = '2.5.3'
        querydslPluginVersion ='1.0.10'
    }
    repositories {
        mavenCentral({
            url "https://plugins.gradle.org/m2/"
        })
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
        classpath("gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:${querydslPluginVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'kotlin-jpa'
    apply plugin: "org.jetbrains.kotlin.plugin.spring"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.kotlin.kapt"

    group = 'com.blog'
    version = '0.0.1-BLOG'
    sourceCompatibility = 11


    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        compileOnly 'org.projectlombok:lombok'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'

        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
        implementation 'org.springframework.boot:spring-boot-starter-webflux'

        // maria db
        implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.4.1'

        // aop
        implementation 'org.springframework.boot:spring-boot-starter-aop'

        implementation("org.jetbrains.kotlin:kotlin-stdlib")
        implementation("org.jetbrains.kotlin:kotlin-reflect")
        implementation("com.fasterxml.jackson.module:jackson-module-kotlin")

        // log4j 취약점 때문에 업그레이드
        implementation 'org.apache.logging.log4j:log4j-api:2.17.0'
        implementation 'org.apache.logging.log4j:log4j-to-slf4j:2.17.0'
        implementation 'org.apache.logging.log4j:log4j-core:2.17.0'

        annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

        // swagger
        implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'
        implementation group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'

        // sentry
        implementation 'io.sentry:sentry-spring-boot-starter:5.6.1'
        implementation 'io.sentry:sentry-logback:5.6.1'
    }

    tasks.test {
        // Use the built-in JUnit support of Gradle.
        useJUnitPlatform()
    }
}

project(':blog-admin') {
    bootJar.enabled = true
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
}

project(':blog-api') {
    bootJar.enabled = true
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
}

project(':blog-domain') {
    bootJar.enabled = false
    jar.enabled = true

    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
}

project(':blog-common') {
    bootJar.enabled = false
    jar.enabled = true

    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
}

def testProjects = [
        project(':blog-api'),
        project(':blog-admin'),
]

configure(testProjects) {
    apply {
        plugin('jacoco')
    }

    jacoco {
        toolVersion = '0.8.5'
    }

    jacocoTestReport {
        executionData(fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec"))
        reports {
            html.enabled true
            xml.enabled true
            csv.enabled false

            xml.destination file("$buildDir/jacoco/jacoco.xml")
        }
        testProjects.forEach {
            sourceSets(it.sourceSets["main"] as SourceSet)
        }
    }

}